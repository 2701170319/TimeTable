apply plugin: 'com.android.application'
apply from: '../properties-util.gradle'
apply plugin: 'walle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.sunrain.timetablev4"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 29
        versionName "2.2.1"
        resConfigs "zh"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (propertyHaveSigningConfigs) {
            release {
                storeFile file(propertyStoreFile)
                storePassword propertyStorePassword
                keyAlias propertyKeyAlias
                keyPassword propertyKeyPassword
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            versionNameSuffix "-debug"
            minifyEnabled false
            shrinkResources false
            if (propertyHaveSigningConfigs) {
                signingConfig signingConfigs.release
            }
        }
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (propertyHaveSigningConfigs) {
                signingConfig signingConfigs.release
            }
        }
    }

    buildTypes.each {
        it.buildConfigField "String", "BUGLY_ID", propertyBuglyId
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'tech.gujin:toast-util:1.1.1'
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.tencent.bugly:crashreport:2.8.6'
    implementation 'com.meituan.android.walle:library:1.1.6'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation 'androidx.core:core:1.0.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.2'
    implementation 'androidx.fragment:fragment:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

walle {
    apkOutputFolder = new File("${project.buildDir}/outputs/apk/release/channels")
    apkFileNameFormat = 'timetable_${versionName}_${channel}.apk'
    channelFile = new File("${project.getProjectDir()}/channel")
}
